# Maintainer: Luca Weiss <luca (at) z3ntu (dot) xyz>

_name=gnumach
pkgname=gnumach-headers
pkgver=1.8.r47.g926090c3
# branch 'master' in hurd/gnumach.git
_commit=926090c399ab503d6f7f67a9556d46b5094fd20f
pkgrel=1
pkgdesc="GNU Mach - header files"
arch=(i686)
url="https://www.gnu.org/software/hurd/microkernel/mach.html"
license=('GPL')
groups=('base-devel')
depends=('glibc')
makedepends=('git')
source=(git+https://git.savannah.gnu.org/git/hurd/gnumach.git#commit=$_commit)
sha512sums=('SKIP')

pkgver() {
  cd $_name
  git describe --long | sed 's/^v//;s/\([^-]*-g\)/r\1/;s/-/./g'
}

prepare() {
  rm -rf build
  mkdir build

  cd $_name
  autoreconf -fi
}

build() {
  cd build

  ../$_name/configure \
    --prefix=/usr \
    --libexecdir=/usr/lib
}

package() {
  cd build
  make DESTDIR=$pkgdir install-data
}
